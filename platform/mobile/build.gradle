apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion build.compileSdkVersion
    buildToolsVersion build.buildToolsVersion
    defaultConfig {
        applicationId "com.phelat.tedu"
        minSdkVersion build.minSdkVersion
        targetSdkVersion build.targetSdkVersion
        versionCode app.versionCode
        versionName app.versionName
        vectorDrawables.useSupportLibrary true
    }
    buildTypes {
        release {
            if (file("google-services.json").exists()) {
                applyFirebasePlugins()
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled false
        }
        debug {
            if (file("google-services.json").exists()) {
                applyFirebasePlugins()
                firebaseCrashlytics {
                    mappingFileUploadEnabled false
                }
            }
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$library.kotlin"

    implementation "androidx.appcompat:appcompat:$library.appcompat"

    implementation "androidx.vectordrawable:vectordrawable:$library.vectordrawable"

    implementation "androidx.navigation:navigation-fragment-ktx:$library.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$library.navigation"

    implementation "com.google.android.material:material:$library.material"

    implementation "com.google.dagger:dagger:$library.dagger"
    implementation "com.google.dagger:dagger-android-support:$library.dagger"

    implementation "com.google.firebase:firebase-analytics:$library.firebaseAnalytics"

    implementation "androidx.multidex:multidex:$library.multidex"

    implementation project(":feature:addtodo")
    implementation project(":feature:todolist")
    implementation project(":feature:settings")
    implementation project(":feature:backup")
    implementation project(":feature:contributors")
    implementation project(":common:designsystem")
    implementation project(':library:navigation')
    implementation project(":library:translation")
    implementation project(":library:dependencyinjection")
    implementation project(":library:androiddagger")
    implementation project(":library:androidcore")

}

preBuild.dependsOn "detekt"

def applyFirebasePlugins() {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}